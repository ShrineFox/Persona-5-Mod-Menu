import("PlayerAppearance.msg"); 
import("Model/PlayerModel.flow");
import("Animation/PlayerAnimation.flow");

void PlayerAppearanceMenu()
{
    while (true)
    {
        int selection = DescriptionMenu(Title_PlayerAppearance, Options_PlayerAppearance);

        switch (selection)
        {
            case -1:
                return;
            case 0:
                // PlayerModel.flow
                PlayerModelMenu();
                break;
            case 1:
                // PlayerAnimation.flow
                PlayerAnimationMenu();
                break;
            case 2:
                ToggleBag();
                break;
            case 3:
                ToggleMorgana();
                break;
            case 4:
                ToggleUmbrella();
                break;
        }
    }
}

void ToggleBag()
{
    int showBag = SystemMenu(Message_PlayerBag, Options_ShowHide);
    if (showBag)
    {
        // Show
        BIT_ON(Flag.Section2 + 110);
        BIT_OFF(Flag.Section2 + 111);
        BIT_OFF(Flag.Section2 + 16);
    }
    else
    {
        // Hide
        BIT_OFF(Flag.Section2 + 110);
        BIT_ON(Flag.Section2 + 111);
        BIT_ON(Flag.Section2 + 16);
    }

    ReloadCurrentField();
}

void ToggleMorgana()
{
    if (FLD_CHECK_MORGANA_BAG() == 1)
    {
        int joker = FLD_PC_GET_CURRENT_RESHND(0);
        int bag = MDL_GET_ITEM_RESHND(joker, 2);

        int showMorgana = SystemMenu(Message_Morgana, Options_ShowHide);
        if (showMorgana)
        {
            // Show
            MDL_ANIM(bag, 50, 0, 0, 1f);
            MDL_ANIM_NEXT(bag, 51, 1, 0, 1f);
        }
        else
        {
            // Hide
            MDL_ANIM(bag, 52, 0, 5, 1f);
            MDL_ANIM_NEXT(bag, 0, 1, 0, 1f);
        }
    }
}

void ToggleUmbrella()
{
    FLD_BAG_ANIM_DISABLE();
    int showUmbrella = SystemMenu(Message_Umbrella, Options_ShowHide);
    if (showUmbrella)
    {
        // Show
        FLD_UMBRELLA_ANIM_CHANGE(1);
    }
    else
    {
        // Hide
        FLD_UMBRELLA_ANIM_CHANGE(0);
    }
}