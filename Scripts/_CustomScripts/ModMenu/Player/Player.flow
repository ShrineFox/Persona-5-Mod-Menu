import("Player.msg");
import("Appearance/PlayerAppearance.flow");
import("Confidants/Confidants.flow");
import("Items/Items.flow");
import("Personas/Personas.flow");
import("Skills/Skills.flow");
import("Stats/Stats.flow");
import("../../Utilities/Utilities.flow");

void PlayerMenu()
{
    while (true && !PAD_CHK_PRESS(PadButton.L2))
    {
        int selection = DescriptionMenu(Title_Player, Options_Player);
        switch (selection)
        {
            case -1:
                return;
            case 0:
                // Appearance/PlayerAppearance.flow
                PlayerAppearanceMenu();
                break;
            case 1:
                SetPlayerName();
                break;
            case 2:
                SetTeamName();
                break;
            case 3:
                PlayerSetMoney();
                break;
            case 4:
                // Personas/Personas.flow
                PersonasMenu();
                break;
            case 5:
                // Personas/Skills.flow
                SkillsMenu();
                break;
            case 6:
                // Items/Items.flow
                ItemsMenu();
                break;
            case 7:
                // Stats/Stats.flow
                StatsMenu();
                break;
            case 8:
                // Confidants/Confidants.flow
                ConfidantsMenu();
                break;
            case 9:
                // Set Security Level
                SetSecurityLevel();
                break;
        }
    }
}

void PlayerSetMoney()
{
    int yen = NumberMenu(7, Title_Yen);

    if (yen > 0)
    {
        GET_MONEY_WINDOW(yen, 0);
        CALL_GLOBAL_MONEY_PANEL();
        CHANGE_GLOBAL_MONEY(yen, 0);
        DEL_GLOBAL_MONEY_PANEL();
    }
}

void SetSecurityLevel()
{
    int security = NumberMenu(3, Title_Security);

    if (security == -1)
        return;
    if (security > 100) 
        security = 100;
    
    //SET_COUNT(131, security);
    //SET_COUNT(135, security);

    FLD_SET_ALERT_VALUE( security );
}

void SetPlayerName()
{
    /* Royal Start */
        BIT_OFF(Flag.Section4 + 276);
    /* Royal End */
    INIT_IME_DRIVER();
    CALL_NAME_ENTRY();
    END_IME_DRIVER();
}

void SetTeamName()
{
    /* Royal Start */
        BIT_OFF(Flag.Section4 + 276);
    /* Royal End */
    INIT_IME_DRIVER();
    CALL_PHANTOM_NAME_ENTRY();
    END_IME_DRIVER();
}