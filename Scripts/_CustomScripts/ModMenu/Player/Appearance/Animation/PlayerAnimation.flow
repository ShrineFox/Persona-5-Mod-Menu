import("PlayerAnimation.msg");

global int gFldAnimGapId;
global int gFldAnimGapIndex;
global float gFldAnimSpeed;
global bool gFldAnimShouldLoop;
global int gFldAnimTime;

void PlayerAnimationMenu()
{
    while (true && !PAD_CHK_PRESS(PadButton.L2))
    {
        int selection = DescriptionMenu(Title_PlayerAnimation, Options_PlayerAnimation);
        switch (selection)
        {
            case -1:
                return;
            case 0:
                PlayerSetFieldGAP();
                break;
            case 1:
                PlayerAnimSelectGAPId();
                break;
            case 2:
                PlayerAnimSelectIndex();
                break;
            case 3:
                PlayerAnimSetLoop();
                break;
            case 4:
                PlayerAnimSetSpeed();
                break;
            case 5:
                PlayerAnimSetTime();
                break;
            case 6:
                PlayerFieldModel_PlayAnim(gFldAnimGapId, gFldAnimGapIndex);
                break;
        }
    }
}

void PlayerSetFieldGAP()
{
    int gapLast = NumberMenu(3, Title_FldGAPID);
    if (gapLast != -1)
    {
        // Confirm entered number
        SET_COUNT(22, gapLast);

        // Reload field to complete GAP swap
        ReloadCurrentField();
    }
}

void PlayerAnimSelectGAPId()
{
    int temp = NumberMenu(3, Title_GAPID);
    if (temp != -1)
        gFldAnimGapId = temp;
}

void PlayerAnimSelectIndex()
{
    int temp = NumberMenu(2, Title_AnimID);
    if (temp != -1)
        gFldAnimGapIndex = temp;
}

void PlayerAnimSetLoop()
{
    gFldAnimShouldLoop = SystemMenu(Message_GapLoop, Options_LoopDont);
}

void PlayerAnimSetSpeed()
{
    float temp = FloatMenu();
    if (temp != -1)
        gFldAnimSpeed = temp / 10;
}

void PlayerAnimSetTime()
{
    int temp = NumberMenu(2, Title_GAPTime);
    if (temp != -1)
        gFldAnimTime = temp * 30;
}

void PlayerFieldModel_PlayAnim(int gapid, int index)
{
    int joker = FLD_PC_GET_CURRENT_RESHND(0);
    int clone = FLD_MODEL_CLONE_ADDMOTION(joker, gapid);
    FLD_UNIT_WAIT_DISABLE(clone);
    MDL_ANIM(clone, index, gFldAnimShouldLoop, 0, gFldAnimSpeed);
    WAIT(gFldAnimTime);
    FLD_MODEL_REVERT_ADDMOTION(joker, clone);
    //FLD_UNIT_SET_WAIT( resHandle );
}