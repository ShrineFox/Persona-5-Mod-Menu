import("Animation/NPCAnimation.flow");
import("NPCs.msg");

// Global Variables
global int gLastSpawnedNpcModelHandle;

void SpawnNPCsMenu()
{
    while (true && !PAD_CHK_PRESS(PadButton.L2))
    {
        int selection = DescriptionMenu(Title_SpawnNPCs, Options_SpawnNPCs);

        switch (selection)
        {
            case -1:
                return;
            case 0: // Spawn Model
                NpcSpawnSelect();
                break;
            case 1: // Animate Model
                // ./Animation/NPCAnimation.flow
                NPCAnimationMenu(gLastSpawnedNpcModelHandle);
                break;
            case 2: // Scale Model
                NpcScale();
                break;
            case 3: // Attach Item Model
                NpcAttachItemModel();
                break;
            case 4: // Spawn Object
                NpcSpawnObjectModel();
                break;
        }
    }
}

void NpcSpawnSelect()
{
    int modelIdMajor = NumberMenu(4, Title_NPCFirst);
    if (modelIdMajor == -1)
        return;

    int modelIdMinor = NumberMenu(3, Title_NPCMiddle);
    if (modelIdMinor == -1)
        return;

    int modelIdSub = NumberMenu(2, Title_NPCLast);
    if (modelIdSub == -1)
        return;

    gLastSpawnedNpcModelHandle = NpcSpawn(modelIdMajor, modelIdMinor, modelIdSub);
}

int NpcSpawn(int modelIdMajor, int modelIdMinor, int modelIdSub)
{
    int pcHandle = FLD_PC_GET_RESHND(0);
    float x = FLD_MODEL_GET_X_TRANSLATE(pcHandle);
    float y = FLD_MODEL_GET_Y_TRANSLATE(pcHandle);
    float z = FLD_MODEL_GET_Z_TRANSLATE(pcHandle);
    float xr = FLD_MODEL_GET_X_ROTATE(pcHandle);
    float yr = FLD_MODEL_GET_Y_ROTATE(pcHandle);
    float zr = FLD_MODEL_GET_Z_ROTATE(pcHandle);

    int npcHandle = FLD_NPC_MODEL_LOAD(modelIdMajor, modelIdMinor, modelIdSub);
    FLD_MODEL_LOADSYNC(npcHandle);
    TransformModel(npcHandle);

    return npcHandle;
}

void NpcScale()
{
    float temp = FloatMenu();
    if (temp == -1)
        return;
    float modelScale = temp / 10;
    FLD_MODEL_SET_SCALE(gLastSpawnedNpcModelHandle, modelScale);
}

void NpcAttachItemModel()
{
    int itemModel = SpawnItemModel(false);
    if (itemModel == -1)
        return;

    AttachModel(gLastSpawnedNpcModelHandle, itemModel);
}

void NpcSpawnObjectModel()
{
    int objModel = SpawnObjectModel(false);
    if (objModel == -1)
        return;

    AttachModel(gLastSpawnedNpcModelHandle, objModel);
}